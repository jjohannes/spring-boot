plugins {
	id "java"
	id "org.springframework.boot.conventions"
}

description = "Spring Boot Server Tests"

configurations {
	testRepository
}

dependencies {
	testImplementation("org.springframework.boot:spring-boot-starter-test")
	testImplementation("org.springframework.boot:spring-boot-actuator-autoconfigure")
	testImplementation("org.springframework.boot:spring-boot-test-support")
	testImplementation("com.samskivert:jmustache")
	testImplementation("jakarta.servlet:jakarta.servlet-api")
	testImplementation("org.apache.httpcomponents:httpasyncclient")
	testImplementation("org.apache.maven.shared:maven-invoker:3.0.0")
	testImplementation("org.awaitility:awaitility")
	testImplementation("org.eclipse.jetty:jetty-webapp") {
		exclude group: "javax.servlet", module: "javax-servlet-api"
	}
	testImplementation("org.springframework:spring-web")

	testRepository(group: "org.springframework.boot", name: "spring-boot-dependencies", configuration: "mavenRepository")
	testRepository(group: "org.springframework.boot", name: "spring-boot-maven-plugin", configuration: "mavenRepository")
	testRepository(group: "org.springframework.boot", name: "spring-boot-starter", configuration: "mavenRepository")
	testRepository(group: "org.springframework.boot", name: "spring-boot-starter-actuator", configuration: "mavenRepository")
	testRepository(group: "org.springframework.boot", name: "spring-boot-starter-jetty", configuration: "mavenRepository")
	testRepository(group: "org.springframework.boot", name: "spring-boot-starter-json", configuration: "mavenRepository")
	testRepository(group: "org.springframework.boot", name: "spring-boot-starter-parent", configuration: "mavenRepository")
	testRepository(group: "org.springframework.boot", name: "spring-boot-starter-tomcat", configuration: "mavenRepository")
	testRepository(group: "org.springframework.boot", name: "spring-boot-starter-undertow", configuration: "mavenRepository")

	testRuntimeOnly("org.springframework.boot:spring-boot-starter-logging")
}

task prepareMavenBinaries(type: org.springframework.boot.build.mavenplugin.PrepareMavenBinaries) {
	outputDir = file("${buildDir}/maven-binaries")
	versions "3.6.2"
}

task syncTestRepository(type: Sync) {
	destinationDir = file("${buildDir}/test-repository")
	from {
		configurations.testRepository
	}
}

test {
	dependsOn prepareMavenBinaries, syncTestRepository
}
