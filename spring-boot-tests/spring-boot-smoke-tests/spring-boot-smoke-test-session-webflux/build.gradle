plugins {
	id "java"
	id "org.springframework.boot.conventions"
}

description = "Spring Boot Session WebFlux smoke test"

def sessionStores = [
	"mongodb": [
		"org.springframework.boot:spring-boot-starter-data-mongodb-reactive",
		"de.flapdoodle.embed:de.flapdoodle.embed.mongo",
		"org.springframework.session:spring-session-data-mongodb"
	],
	"redis": [
		"org.springframework.boot:spring-boot-starter-data-redis-reactive",
		"org.springframework.session:spring-session-data-redis"
	]
]

dependencies {
	implementation("org.springframework.boot:spring-boot-starter-security")
	implementation("org.springframework.boot:spring-boot-starter-webflux")

	sessionStores[project.findProperty("sessionStore") ?: "mongodb"].each { runtimeOnly it }

	testImplementation("org.springframework.boot:spring-boot-starter-test")
}
